<G> ::= (<FunctionDeclaration> | <CompoundStatement>)+

<FunctionDeclaration> ::= "россии_нужен"
            <Identifier> "за" (<Identifier>)+ "почти_без_переплат"
            <StatementBlock>

<CompoundStatement> ::= <StatementBlock> | <SingleStatement>

<StatementBlock> ::= "олег_не_торопись"
                        (<SingleStatement>)+
                     "я_олигарх_мне_заебись"

<SingleStatement> ::= <While>
                    | <IfElse>
                    | <DoIf>
                    | <Assign> "сомнительно_но_окей"
                    | <Return> "сомнительно_но_окей"

<While> ::= "ну_сколько можно" <MathExpr> "?"
                <CompoundStatement>

<IfElse> ::= "какая_разница" <MathExpr> "?"
                <CompoundStatement>
             ("я_могу_ошибаться"
                <CompoundStatement>)?

<DoIf> ::=  "я_ссыкло_или_я_не_ссыкло"
                <CompoundStatement>
            "какая_разница" <MathExpr> "?"

<Assign> ::= <VariableDeclarator>?
         <Lvalue> "я_так_чувствую" <Rvalue>

<Return> ::= "никто_никогда_не_вернет" <Identifier>

<Lvalue> ::= <Identifier>
<Rvalue> ::= <MathExpr>
<VariableDeclarator> ::= "але_здравствуйте_меня_зовут_ольга_звоню_вам_из_тинькок_банка_вам_удобно_сейчас_разговаривать"

<MathExpr>  ::= <AddSubRes> ("<" | "<=" | "==" | ">=" | ">" | "!=") <AddSubRes>
<AddSubRes> ::= <MulDivRes> (("+" | "-") <MulDivRes>)*
<MulDivRes> ::= <PowRes> (("*" | "/") <PowRes>)*
<PowRes>    ::= <Operand> ("^" <Operand>)?
<Operand>   ::= "$" <MathExpr> "$" | <SimpleOperand>

<Identifier>    ::= ([A-Z] | [А-Я] | [a-z] | [а-я] | "_") ([A-Z] | [А-Я] | [a-z] | [а-я] | "_" | [0-9])*
<Number>        ::= ([0-9])+
<SimpleOperand> ::= <Identifier> | <Number>
